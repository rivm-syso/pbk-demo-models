model simple_dermal()

  # Parameters

  ## Physiology

  ### Body size
  BW = 70               # body weight [kg]
  BSA = 190             # body surface area [dm2]
  VBody = 1             # total body density [L/kg]
  VBc = 0.074           # blood volume as fraction of body weight [L/kg]
  VLc = 0.0257          # liver volume as fraction of body weight [L/kg]

  ### Skin parameters
  FSkinExposed = 0.1    # fraction of skin exposed [unitless]
  SkinThickness = 0.1   # skin thickness [dm]

  ### Flow rates
  QCC = 15              # cardiac output as function of body weight [L/h/kg^0.75]
  QRc = 0.715           # fraction of blood flow to rest-of-body [-]
  QLc = 0.227           # fraction of blood flow to liver [-]
  QSkc = 0.058          # fraction of blood flow to skin [-]

  ## Biochemical parameters

  #### Partition coefficients
  PCLiver = 2           # partition coefficient liver:blood [-]
  PCRest = 1.2          # partition coefficient rest:blood [-]
  PCSkin = 0.1          # partition coefficient skin:blood [-]

  #### Metabolism and excretion
  CLUrine = 10          # urinary excretion rate [L/h]

  # Assignments

  ## Physiology

  ### Flow rates
  const QC := QCC * BW^0.75   # cardiac output adjusted for body weight [L/h]
  const QL := QC * QLc        # blood flow to liver [L/h]
  const QR := QC * QRc        # blood flow to rest-of-body [L/h]
  const QSk := QC * QSkc      # blood flow to skin [L/h]

  # Compartments
  compartment Skin := BSA * FSkinExposed * SkinThickness
  compartment Blood := BW * VBc
  compartment Liver := BW * VLc
  compartment Rest := BW * VBody - Blood - Liver - Skin
  compartment Urine

  # Species
  substanceOnly species ASkin in Skin
  substanceOnly species ABlood in Blood
  substanceOnly species ALiver in Liver
  substanceOnly species ARest in Rest
  substanceOnly species AUrine in Urine

  # Initial amounts
  ASkin = 0
  ABlood = 0
  ALiver = 0
  ARest = 0
  AUrine = 0

  # Model equations

  ## Absorption
  ASkin -> ABlood; QSk * (ASkin / Skin) / PCSkin

  ## Distribution
  ABlood -> ALiver; QL * (ABlood / Blood)
  ALiver -> ABlood; QL * (ALiver / Liver) / PCLiver
  ABlood -> ARest; QR * (ABlood / Blood)
  ABlood -> ASkin; QSk * (ABlood / Blood)
  ARest -> ABlood; QR * (ARest / Rest) / PCRest

  ## Excretion
  ABlood -> AUrine; (ABlood / Blood) * CLUrine

end
